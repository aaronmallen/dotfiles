#!/usr/bin/env zsh

if [[ "$ZSH_BENCHMARK" == "true" ]]; then
  if [[ -z "$ZSH_START_TIME" ]]; then
    ZSH_START_TIME=$(date +%s.%N)
  fi
fi

zsh_debug() {
  [[ "$ZSH_DEBUG" == "true" ]] && printf "loading %s from %s\n" "$1" "$2"
}

[ -f "$HOME/.config/xdg/env.sh" ] && source "$HOME/.config/xdg/env.sh"

# set ZSH directories
export ZDOTDIR=${XDG_CONFIG_HOME:-$HOME/.config}/zsh
export ZSH=$ZDOTDIR

# Initialize Homebrew first if available (critical for other tools)
if [ -f /opt/homebrew/bin/brew ]; then
  export PATH="/opt/homebrew/bin:$PATH"
  eval "$(brew shellenv)"
fi

# all of our .zsh files
typeset -U config_files
setopt extended_glob
config_files=(${XDG_CONFIG_HOME:-$HOME/.config}/**/*.{sh,zsh}~*/claude/shell-snapshots/*~*/claude/plugins/*)

# load the env files first
for file in ${(M)config_files:#*/env.sh}
do
  zsh_debug "env" "$file"
  source $file
done

# load the path files
for file in ${(M)config_files:#*/path.sh}
do
  zsh_debug "path" "$file"
  source $file
done

# load everything but the env, path and completion files
for file in ${${${config_files:#*/env.sh}:#*/path.sh}:#*/completion.zsh}
do
  zsh_debug "${file:t}" "$file"
  source $file
done

# if a secrets files exists load them
for secrets in zsh/secrets zsh/secrets.local; do
  file="${XDG_CONFIG_HOME:-$HOME/.config}/$secrets"
  if [[ -f "$file" ]]; then
    zsh_debug "secrets" "$file"
    source "$file"
  fi
done

# add completion directories to fpath before compinit
for file in ${(M)config_files:#*/completion.zsh}
do
  fpath=(${file:h} $fpath)
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -Uz compinit
compinit -d "${XDG_CONFIG_HOME:-$HOME/.config}"/zsh/zcompdump-"$ZSH_VERSION"

# source every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  zsh_debug "completion" "$file"
  source $file
done

unset config_files

if [[ "$ZSH_BENCHMARK" == "true" ]]; then
  ZSH_END_TIME=$(date +%s.%N)
  ZSH_LOAD_TIME=$(printf "%.2f" "$(echo "$ZSH_END_TIME - $ZSH_START_TIME" | bc)")
  echo "zsh loaded in ${ZSH_LOAD_TIME} seconds"

  unset ZSH_START_TIME ZSH_END_TIME ZSH_LOAD_TIME ZSH_BENCHMARK ZSH_DEBUG
fi
